#version 320 es

//============================================================================================================
//
//
//                  Copyright (c) 2024, Qualcomm Innovation Center, Inc. All rights reserved.
//                              SPDX-License-Identifier: BSD-3-Clause
//
//============================================================================================================

float FastLanczos(float base)
{
	float y = base - 1.0f;
	float y2 = y * y;
	float y_temp = 0.75f * y + y2;
	return y_temp * y2;
}

vec3 DecodeColor(uint sample32)
{
	uint x11 = sample32 >> 21u;
	uint y11 = sample32 & (2047u << 10u);
	uint z10 = sample32 & 1023u;
	vec3 samplecolor;
	samplecolor.x = (float(x11) * (1.0 / 2047.5));
	samplecolor.y = (float(y11) * (4.76953602e-7)) - 0.5;
	samplecolor.z = (float(z10) * (1.0 / 1023.5)) - 0.5;

	return samplecolor;
}

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(set = 0, binding = 1) uniform highp sampler2D PrevHistoryOutput;
layout(set = 0, binding = 2) uniform highp sampler2D MotionDepthClipAlphaBuffer;
layout(set = 0, binding = 3) uniform highp usampler2D YCoCgColor;
layout(set = 0, binding = 4, rgba16f) uniform writeonly mediump image2D HistoryOutput;
layout(set = 0, binding = 5, rgba16f) uniform writeonly mediump image2D SceneColorOutput;

layout(std140, set = 0, binding = 0) uniform readonly Params
{
    uvec2                renderSize;
    uvec2                displaySize;
    vec2                 renderSizeRcp;
    vec2                 displaySizeRcp;
    vec2                 jitterOffset;
    vec2                 padding1;
    vec4                 clipToPrevClip[4];
    float                preExposure;
    float                cameraFovAngleHor;
    float                cameraNear;
    float                MinLerpContribution;
    uint                 bSameCamera;
    uint                 reset;
} params;

void main()
{
    float Biasmax_viewportXScale = min(float(params.displaySize.x) / float(params.renderSize.x), 1.99);  //Biasmax_viewportXScale
    float scalefactor = min(20.0, pow((float(params.displaySize.x) / float(params.renderSize.x)) * (float(params.displaySize.y) / float(params.renderSize.y)), 3.0));
    float f2 = params.preExposure;            //1.0;   //preExposure
    vec2 HistoryInfoViewportSizeInverse = params.renderSizeRcp;
    vec2 HistoryInfoViewportSize = vec2(params.displaySize);
    vec2 InputJitter = params.jitterOffset;
    vec2 InputInfoViewportSize = vec2(params.renderSize);
    vec2 Hruv = (vec2(gl_GlobalInvocationID.xy) + vec2(0.5)) * HistoryInfoViewportSizeInverse;
    vec2 Jitteruv;
    Jitteruv.x = clamp(Hruv.x + (InputJitter.x * HistoryInfoViewportSizeInverse.x), 0.0, 1.0);
    Jitteruv.y = clamp(Hruv.y + (InputJitter.y * HistoryInfoViewportSizeInverse.y), 0.0, 1.0);

    ivec2 InputPos = ivec2(Jitteruv * InputInfoViewportSize);

    //vec2 Motion = texelFetch(MotionDepthClipAlphaBuffer, InputPos, 0).xy;
    float alphab = texelFetch(MotionDepthClipAlphaBuffer, InputPos, 0).w;
    vec3 mda = textureLod(MotionDepthClipAlphaBuffer, Jitteruv, 0.0).xyz;
    vec2 Motion = mda.xy;

    ///ScreenPosToViewportScale&Bias
    vec2 PrevUV;
    PrevUV.x = clamp(-0.5 * Motion.x + Hruv.x, 0.0, 1.0);
#ifdef REQUEST_NDC_Y_UP
    PrevUV.y = clamp(0.5 * Motion.y + Hruv.y, 0.0, 1.0);
#else
    PrevUV.y = clamp(-0.5 * Motion.y + Hruv.y, 0.0, 1.0);
#endif

    float depthfactor = mda.z;
    float history_value = fract(alphab); // clamp(alpha, 0.0f, 1.0f);
    float alphamask = (alphab - history_value) * 0.001f;
    history_value *= 2.0;

    vec4 History = textureLod(PrevHistoryOutput, PrevUV, 0.0);
    vec3 HistoryColor = History.xyz;
    float Historyw = History.w;
    float Wfactor = max(clamp(abs(Historyw), 0.0, 1.0), alphamask);

    /////upsample and compute box
    vec4 Upsampledcw = vec4(0.0);
    float kernelfactor = clamp(Wfactor + float(params.reset), 0.0, 1.0);
    float biasmax = Biasmax_viewportXScale - Biasmax_viewportXScale * kernelfactor;
    float biasmin = max(1.0f, 0.3 + 0.3 * biasmax);
    float biasfactor = max(0.25f * depthfactor, kernelfactor);
    float kernelbias = mix(biasmax, biasmin, biasfactor);
    float motion_viewport_len = length(Motion * HistoryInfoViewportSize);
    float curvebias = mix(-2.0, -3.0, clamp(motion_viewport_len * 0.02, 0.0, 1.0));

    vec3 rectboxcenter = vec3(0.0);
    vec3 rectboxvar = vec3(0.0);
    float rectboxweight = 0.0;
    vec2 srcpos = vec2(InputPos) + vec2(0.5) - InputJitter;
    vec2 srcOutputPos = Hruv * InputInfoViewportSize;

    kernelbias *= 0.5f;
    float kernelbias2 = kernelbias * kernelbias;
    vec2 srcpos_srcOutputPos = srcpos - srcOutputPos;

    ivec2 InputPosBtmRight = ivec2(1) + InputPos;
    vec2 gatherCoord = vec2(InputPos) * params.renderSizeRcp;
    uint btmRight = texelFetch(YCoCgColor, InputPosBtmRight, 0).x;
    uvec4 topleft = textureGather(YCoCgColor, gatherCoord);
    uvec2 topRight = textureGather(YCoCgColor, gatherCoord + vec2(params.renderSizeRcp.x, 0.0)).yz;
    uvec2 bottomLeft = textureGather(YCoCgColor, gatherCoord + vec2(0.0, params.renderSizeRcp.y)).xy;

    vec3 rectboxmin;
    vec3 rectboxmax;
    {
        rectboxmin = DecodeColor(btmRight);
        vec2 baseoffset = srcpos_srcOutputPos + vec2(1.0, 1.0);
        float baseoffset_dot = dot(baseoffset, baseoffset);
        float base = clamp(baseoffset_dot * kernelbias2, 0.0, 1.0);
        float weight = FastLanczos(base);
        Upsampledcw += vec4(rectboxmin * weight, weight);
        float boxweight = exp(baseoffset_dot * curvebias);
        rectboxmax = rectboxmin;
        vec3 wsample = rectboxmin * boxweight;
        rectboxcenter = wsample;
        rectboxvar = rectboxmin * wsample;
        rectboxweight = boxweight;
    }
    {
        vec3 samplecolor = DecodeColor(bottomLeft.x);
        vec2 baseoffset = srcpos_srcOutputPos + vec2(-1.0, 1.0);
        float baseoffset_dot = dot(baseoffset, baseoffset);
        float base = clamp(baseoffset_dot * kernelbias2, 0.0f, 1.0f);
        float weight = FastLanczos(base);
        Upsampledcw += vec4(samplecolor * weight, weight);
        float boxweight = exp(baseoffset_dot * curvebias);
        rectboxmin = min(rectboxmin, samplecolor);
        rectboxmax = max(rectboxmax, samplecolor);
        vec3 wsample = samplecolor * boxweight;
        rectboxcenter += wsample;
        rectboxvar += (samplecolor * wsample);
        rectboxweight += boxweight;
    }
    {
        vec3 samplecolor = DecodeColor(bottomLeft.y);
        vec2 baseoffset = srcpos_srcOutputPos + vec2(0.0, 1.0);
        float baseoffset_dot = dot(baseoffset, baseoffset);
        float base = clamp(baseoffset_dot * kernelbias2, 0.0f, 1.0f);
        float weight = FastLanczos(base);
        Upsampledcw += vec4(samplecolor * weight, weight);
        float boxweight = exp(baseoffset_dot * curvebias);
        rectboxmin = min(rectboxmin, samplecolor);
        rectboxmax = max(rectboxmax, samplecolor);
        vec3 wsample = samplecolor * boxweight;
        rectboxcenter += wsample;
        rectboxvar += (samplecolor * wsample);
        rectboxweight += boxweight;
    }
    {
        vec3 samplecolor = DecodeColor(topRight.x);
        vec2 baseoffset = srcpos_srcOutputPos + vec2(1.0, 0.0);
        float baseoffset_dot = dot(baseoffset, baseoffset);
        float base = clamp(baseoffset_dot * kernelbias2, 0.0f, 1.0f);
        float weight = FastLanczos(base);
        Upsampledcw += vec4(samplecolor * weight, weight);
        float boxweight = exp(baseoffset_dot * curvebias);
        rectboxmin = min(rectboxmin, samplecolor);
        rectboxmax = max(rectboxmax, samplecolor);
        vec3 wsample = samplecolor * boxweight;
        rectboxcenter += wsample;
        rectboxvar += (samplecolor * wsample);
        rectboxweight += boxweight;
    }
    {
        vec3 samplecolor = DecodeColor(topRight.y);
        vec2 baseoffset = srcpos_srcOutputPos + vec2(1.0, -1.0);
        float baseoffset_dot = dot(baseoffset, baseoffset);
        float base = clamp(baseoffset_dot * kernelbias2, 0.0f, 1.0f);
        float weight = FastLanczos(base);
        Upsampledcw += vec4(samplecolor * weight, weight);
        float boxweight = exp(baseoffset_dot * curvebias);
        rectboxmin = min(rectboxmin, samplecolor);
        rectboxmax = max(rectboxmax, samplecolor);
        vec3 wsample = samplecolor * boxweight;
        rectboxcenter += wsample;
        rectboxvar += (samplecolor * wsample);
        rectboxweight += boxweight;
    }
    {
        vec3 samplecolor = DecodeColor(topleft.x);
        vec2 baseoffset = srcpos_srcOutputPos + vec2(-1.0, 0.0);
        float baseoffset_dot = dot(baseoffset, baseoffset);
        float base = clamp(baseoffset_dot * kernelbias2, 0.0f, 1.0f);
        float weight = FastLanczos(base);
        Upsampledcw += vec4(samplecolor * weight, weight);
        float boxweight = exp(baseoffset_dot * curvebias);
        rectboxmin = min(rectboxmin, samplecolor);
        rectboxmax = max(rectboxmax, samplecolor);
        vec3 wsample = samplecolor * boxweight;
        rectboxcenter += wsample;
        rectboxvar += (samplecolor * wsample);
        rectboxweight += boxweight;
    }
    {
        vec3 samplecolor = DecodeColor(topleft.y);
        vec2 baseoffset = srcpos_srcOutputPos;
        float baseoffset_dot = dot(baseoffset, baseoffset);
        float base = clamp(baseoffset_dot * kernelbias2, 0.0f, 1.0f);
        float weight = FastLanczos(base);
        Upsampledcw += vec4(samplecolor * weight, weight);
        float boxweight = exp(baseoffset_dot * curvebias);
        rectboxmin = min(rectboxmin, samplecolor);
        rectboxmax = max(rectboxmax, samplecolor);
        vec3 wsample = samplecolor * boxweight;
        rectboxcenter += wsample;
        rectboxvar += (samplecolor * wsample);
        rectboxweight += boxweight;
    }
    {
        vec3 samplecolor = DecodeColor(topleft.z);
        vec2 baseoffset = srcpos_srcOutputPos + vec2(0.0, -1.0);
        float baseoffset_dot = dot(baseoffset, baseoffset);
        float base = clamp(baseoffset_dot * kernelbias2, 0.0f, 1.0f);
        float weight = FastLanczos(base);
        Upsampledcw += vec4(samplecolor * weight, weight);
        float boxweight = exp(baseoffset_dot * curvebias);
        rectboxmin = min(rectboxmin, samplecolor);
        rectboxmax = max(rectboxmax, samplecolor);
        vec3 wsample = samplecolor * boxweight;
        rectboxcenter += wsample;
        rectboxvar += (samplecolor * wsample);
        rectboxweight += boxweight;
    }
    {
        vec3 samplecolor = DecodeColor(topleft.w);
        vec2 baseoffset = srcpos_srcOutputPos + vec2(-1.0, -1.0);
        float baseoffset_dot = dot(baseoffset, baseoffset);
        float base = clamp(baseoffset_dot * kernelbias2, 0.0f, 1.0f);
        float weight = FastLanczos(base);
        Upsampledcw += vec4(samplecolor * weight, weight);
        float boxweight = exp(baseoffset_dot * curvebias);
        rectboxmin = min(rectboxmin, samplecolor);
        rectboxmax = max(rectboxmax, samplecolor);
        vec3 wsample = samplecolor * boxweight;
        rectboxcenter += wsample;
        rectboxvar += (samplecolor * wsample);
        rectboxweight += boxweight;
    }

    rectboxweight = 1.0 / rectboxweight;
    rectboxcenter *= rectboxweight;
    rectboxvar *= rectboxweight;
    rectboxvar = sqrt(abs(rectboxvar - rectboxcenter * rectboxcenter));

    Upsampledcw.xyz =  clamp(Upsampledcw.xyz / Upsampledcw.w, rectboxmin-vec3(0.05), rectboxmax+vec3(0.05));
    Upsampledcw.w = Upsampledcw.w * (1.0f / 3.0f) ;

	float tcontribute = history_value * clamp(rectboxvar.x * 10.0f, 0.0, 1.0);
	float OneMinusWfactor = 1.0f - Wfactor;
	tcontribute = tcontribute * OneMinusWfactor;

    float baseupdate = OneMinusWfactor - OneMinusWfactor * depthfactor;
    baseupdate = min(baseupdate, mix(baseupdate, Upsampledcw.w *10.0f, clamp(10.0f* motion_viewport_len, 0.0, 1.0)));
    baseupdate = min(baseupdate, mix(baseupdate, Upsampledcw.w, clamp(motion_viewport_len *0.05f, 0.0, 1.0)));
    float basealpha = baseupdate;

    const float EPSILON = 1.192e-07f;
    float boxscale = max(depthfactor, clamp(motion_viewport_len * 0.05f, 0.0, 1.0));
    float boxsize = mix(scalefactor, 1.0f, boxscale);
    vec3 sboxvar = rectboxvar * boxsize;
    vec3 boxmin = rectboxcenter - sboxvar;
    vec3 boxmax = rectboxcenter + sboxvar;
    rectboxmax = min(rectboxmax, boxmax);
    rectboxmin = max(rectboxmin, boxmin);

    vec3 clampedcolor = clamp(HistoryColor, rectboxmin, rectboxmax);
	float lerpcontribution = (any(greaterThan(rectboxmin, HistoryColor)) || any(greaterThan(HistoryColor, rectboxmax))) ? tcontribute : 1.0f;
	lerpcontribution = lerpcontribution - lerpcontribution * sqrt(alphamask);
	HistoryColor = mix(clampedcolor, HistoryColor, clamp(lerpcontribution, 0.0, 1.0));
    float basemin = min(basealpha, 0.1f);
    basealpha = mix(basemin, basealpha, clamp(lerpcontribution, 0.0, 1.0));

    ////blend color
    float alphasum = max(EPSILON, basealpha + Upsampledcw.w);
    float alpha = clamp(Upsampledcw.w / alphasum + float(params.reset), 0.0, 1.0);
    Upsampledcw.xyz = mix(HistoryColor, Upsampledcw.xyz, alpha);

    imageStore(HistoryOutput, ivec2(gl_GlobalInvocationID.xy), vec4(Upsampledcw.xyz, Wfactor));

    ////ycocg to grb
    float x_z = Upsampledcw.x - Upsampledcw.z;
    Upsampledcw.xyz = vec3(
        x_z + Upsampledcw.y,
        Upsampledcw.x + Upsampledcw.z,
        x_z - Upsampledcw.y);

    float compMax = max(Upsampledcw.x, Upsampledcw.y);
    compMax = max(compMax, Upsampledcw.z);
    float scale = params.preExposure /  ((1.0f + 1.0f / 65504.0f) - compMax);   //(1.0f + 1.0f / 65504.0f) = 1.000015e+00

    Upsampledcw.xyz = Upsampledcw.xyz * scale;
    imageStore(SceneColorOutput, ivec2(gl_GlobalInvocationID.xy), Upsampledcw);

}